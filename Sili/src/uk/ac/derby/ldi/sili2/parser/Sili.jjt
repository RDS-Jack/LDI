/*
 * A simple expression parser and evaluator
 *
 */

options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
  MULTI = true;
  VISITOR = true;
  JDK_VERSION = "1.8";
  NODE_DEFAULT_VOID = true;
  NODE_EXTENDS = "uk.ac.derby.ldi.sili2.interpreter.BaseASTNode";
  NODE_PACKAGE = "uk.ac.derby.ldi.sili2.parser.ast";
  JJTREE_OUTPUT_DIRECTORY = "ast";
}

PARSER_BEGIN(Sili)
package uk.ac.derby.ldi.sili2.parser.ast;
public class Sili {}
PARSER_END(Sili)

/*
 *
 * Lexer definitions
 *
 */

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN [IGNORE_CASE] : /* RESERVED WORDS AND LITERALS */
{

 < RETURN: "RETURN" >
| < NOT: "NOT" >
| < TRUE: "TRUE" >
| < FALSE: "FALSE" >
}

TOKEN : /* RESERVED WORDS AND LITERALS WITH CASE */
{
   < PLUSENG:"PLUS">
| < MINUSENG:"MINUS">
| < MULENG:"MULTIPLY">
| < DIVENG:"DIVIDE">
| < MODULUS:"MODULUS">
| < ASSIGNENG:"ASSIGN TO">
| < AND:"AND"|"&">
| < OR: "OR"|"|" >
| < GTENG: "GREATER THAN"|">" >
| < LTENG: "LESS THAN"|"<" >
| < EQENG: "EQUALS"|"==" >
| < GEQENG: "GREATER THAN OR EQUAL"|">=" >
| < LEQENG: "LESS THAN OR EQUAL"|"<=" >
| < NEENG: "NOT EQUAL"|"!=" >
| < QUIT: "QUIT">
| < ARRAY: "Array">
| < CLASS:"Class">
| <CREATE:"Create">
| <FROM:"From">
| <RANDOM:"RandomNumber">
| < FOR: "FOR" >
| <WHILE: "WHILE" >
| < IF: "IF" >
| < ELSE: "ELSE" >
| < FN: "Function" >
| <WRITE:"Output">
}

TOKEN : /* RESERVED TYPES */
{
	<Integer:"Integer">
| 	<String:"String" >
|   <Boolean:"Boolean">
|   <Double:"Double">
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < STRING_LITERAL:
     ( 
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
     )
   | (
      "'"
      (   (~["\'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "'"
     )
  >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <UPPERCASE_LETTER> (<LETTER>|<DIGIT>|"#")* >
| < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
|
	< #UPPERCASE_LETTER:
		
						  ["A" - "Z"]
		
	>
}

TOKEN : /* SEPARATORS */
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < PP: "++" >
| < MM: "--" >
}

TOKEN : /* OPERATOR SYMBOLS */
{
  < ASSIGN: "=" >
| < PEQ: "+=" >
| < MEQ: "-=" >
| < UEQ: "@=" >
| < HOOK: "?" >
| < COLON: ":" >
| < PLUS: "+" >
| < SUBT: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < REM: "%" >
}

/***********************************************
 *          THE GRAMMAR STARTS HERE            *
 ***********************************************/

ASTCode code() #Code :
{}
{
     			      statement() ( statement() )* <EOF>  {return jjtThis;}
}

void statement() #Statement :
{}
{
				 	LOOKAHEAD(2) assignment()
				|	fndef()
				|	block()
				|	ifstatement()	
				|	forloop()
				|   whileloop()
				|	write()
				|	call()
				|   quit()
				|   typedAssignment()
				|   classAssignment()
				|   instantiateObject()
}

void block() #Block :
{}
{
					"{" ( statement() )* "}"
}

void parmlist() #Parmlist :
{}
{
					"(" [ identifier() ( "," identifier() )* ] ")"
}

void fnbody() #FnBody :
{}
{
					( statement() )* 
}

void fndef() #FnDef :
{}
{
					<FN> identifier() parmlist() "{" 
						fnbody()
						[ <RETURN> returnexpr() ";" {jjtThis.fnHasReturn = true;} ] 
					"}"
}

void returnexpr() #ReturnExpression :
{}
{
					expression()
}

void assignment() :
{}
{
					identifier() (("="|<ASSIGNENG>) expression() ";" #Assignment(2)
								| "." identifier() ("="|<ASSIGNENG>) expression() ";" #SetObjectVar(3)
								| "[" integer() "]" ("="|<ASSIGNENG>) expression() ";" #SetArrayIndex(3))
}

void typedAssignment() :
{}
{
					types() (identifier() (("="|<ASSIGNENG>) expression() ";" #TypedAssignment(3)
											| ";" #ClassVarAssignment(2))
			| 		    	<ARRAY> identifier() ("="|<ASSIGNENG>) arrayArgList() ";" #ArrayAssignment(3))
}

void classAssignment() #ClassAssignment :
{}
{
					<CLASS> identifier() "{" typedAssignment() (typedAssignment())* "}"
}

void ifstatement() #IfStatement :
{}
{
  					<IF> "(" expression() ")" statement() 
  					[ LOOKAHEAD(1) <ELSE> statement() {jjtThis.ifHasElse = true;} ]
}

void forloop() #ForLoop :
{}
{
  					<FOR> "(" assignment() expression() ";" assignment() ")" statement()
}

void whileloop() #WhileLoop :
{}
{
  					<WHILE> "(" expression() ")" statement()
}
void write() :
{}
{
					<WRITE> expression() ";" #Write(1)
}

void call() #Call :
{}
{
					identifier() arglist()
}

void arglist() #ArgList :
{}
{
					"(" [ expression() ] ( "," expression() )* ")"
}

void arrayArgList() #ArrayArgList :
{}
{
  					"{" [ expression()] ("," expression())* "}"
}

void instantiateObject() #InstantiateObject :
{}
{
			<CREATE> identifier() <FROM> identifier() ";"
}

//
// Expressions
//

void expression() :
{}
{
                  or_expression()
}

void or_expression() : 
{} 
{ 
			      and_expression() ( <OR> and_expression() #Or(2) )* 
}

void and_expression() : 
{} 
{ 
			      compare_expression() ( <AND> compare_expression() #And(2) )*
}

void compare_expression() : 
{} 
{ 
                              add_expression() [
                                 <EQENG>  add_expression() #CompEqual(2)
                               | <NEENG> add_expression() #CompNequal(2)
                               | <GEQENG> add_expression() #CompGTE(2)
                               | <LEQENG>  add_expression() #CompLTE(2)
                               | <GTENG>  add_expression() #CompGT(2)
                               | <LTENG>  add_expression() #CompLT(2)
                               ]
}

void add_expression() : 
{} 
{
                              mul_expression() (
                                  (                                    "+"|<PLUSENG>)  mul_expression() #Add(2)
                                | ("-"|<MINUSENG>)  mul_expression() #Subtract(2)
                              )*
}

void mul_expression() : 
{} 
{ 
			      			  unary_expression() ( 
                                  (                                    "*"|<MULENG>) unary_expression() #Times(2)
                                | ("/"|<DIVENG>) unary_expression() #Divide(2)
                                | ("%"|<MODULUS>) unary_expression() #Modulus(2)
                              )*
}

void unary_expression() :
{}
{
                              <NOT> unary_expression() #UnaryNot(1)
                            | ("+"|<PLUSENG>)   unary_expression() #UnaryPlus(1)
                            | ("-"|<MINUSENG>)   unary_expression() #UnaryMinus(1)
                            | primary_expression()
}

void primary_expression() : 
{}
{
                              literal()
                            | LOOKAHEAD(2) invoke()
                            | dereference()
                            | "(" expression() ")"
                            | random()
}

void invoke() :
{}
{
							identifier() (arglist() #FnInvoke(2)
							| "[" integer() "]" #ArrayDereference(2)
							|  "." identifier() #ClassDereference(2))
}

void random() #RandomNumber:
{}
{
  							<RANDOM>"(" expression() "," expression() ")"
}

void literal() :
{}
{
							  integer()
                            | character()							  	
							| rational()
							| bool()						
}
void types() :
{}
{
							  intType()
							| stringType()
							| boolType()
							| doubleType()
}

void identifier() #Identifier :
{Token t;}
{
	t = <IDENTIFIER> {jjtThis.tokenValue = t.image;}
}

void dereference() #Dereference :
{Token t;}
{
	t = <IDENTIFIER> {jjtThis.tokenValue = t.image;}
}

void character() #Character :
{Token t;}
{
	t = <STRING_LITERAL> {jjtThis.tokenValue = t.image;}
}

void integer() #Integer : 
{Token t;} 
{ 
	t = <INTEGER_LITERAL> {jjtThis.tokenValue = t.image;}
}

void rational() #Rational : 
{Token t;} 
{ 
	t = <FLOATING_POINT_LITERAL> {jjtThis.tokenValue = t.image;}
}

void bool() : 
{} 
{ 
			      <TRUE> #True
                | <FALSE> #False
}

void quit() :
{}
{
  					<QUIT> #Quit
}
void intType() #IntegerType :
{Token t;}
{
  	t = <Integer> { jjtThis.tokenValue = t.image;}
}
void stringType() #StringType :
{Token t; }
{
	t = <String> { jjtThis.tokenValue = t.image;}
}
void boolType() #BoolType :
{Token t; }
{
	t = <Boolean> { jjtThis.tokenValue = t.image;}
}
void doubleType() #DoubleType :
{Token t; }
{
	t = <Double> { jjtThis.tokenValue = t.image;}
}
